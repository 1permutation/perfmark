buildscript {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath 'com.github.erizo.gradle:jcstress-gradle-plugin:0.8.3'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.6'
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.8"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: 'maven-publish'
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: "me.champeau.gradle.jmh"
    apply plugin: "net.ltgt.errorprone"

    repositories {
        maven {
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        mavenLocal()
    }

    publishing {
        publications {
            myLibrary(MavenPublication) {
                from components.java
            }
        }

        repositories {
            mavenLocal()
        }
    }

    if (rootProject.properties.get('errorProne', true)) {
        dependencies {
            errorprone 'com.google.errorprone:error_prone_core:2.3.3'
            errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
        }
    } else {
        // Disable Error Prone
        allprojects {
            afterEvaluate { project ->
                project.tasks.withType(JavaCompile) {
                    options.errorprone.enabled = false
                }
            }
        }
    }

    jacoco { toolVersion = "0.8.2" }

    group = "io.perfmark"
    version = "1.0.0-SNAPSHOT"

    ext {
        libraries = [
                errorprone: 'com.google.errorprone:error_prone_annotations:[2.3.2,2.3.3]',
                jsr305: 'com.google.code.findbugs:jsr305:3.0.2',
                gson: 'com.google.code.gson:gson:2.8.5'
        ]
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}
